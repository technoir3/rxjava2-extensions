buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "java-library"
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
    id "com.github.ben-manes.versions" version "0.21.0"
}

version = "0.0.7"

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk7"
    api "io.reactivex.rxjava2:rxjava:2.2.7"

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.4.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.4.0"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform()
}

tasks.register("packageJavadoc", Jar.class) {
    dependsOn tasks.named("javadoc")
    from javadoc.destinationDir
    archiveClassifier.set("javadoc")
}

tasks.register("packageSources", Jar.class) {
    dependsOn tasks.named("classes")
    from sourceSets.main.allSource
    archiveClassifier.set("sources")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "com.sch.rxjava"
            artifactId "rxjava2-extensions"
            from components.java

            artifact packageJavadoc
            artifact packageSources
        }
    }
}

bintray {
    user = findProperty("BINTRAY_USER")
    key = findProperty("BINTRAY_KEY")
    publish = true

    pkg {
        repo = "maven"
        name = "rxjava2-extensions"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://github.com/technoir42/rxjava2-extensions.git"
        version {
            name = project.version
        }
        publications = ["maven"]
    }
}
